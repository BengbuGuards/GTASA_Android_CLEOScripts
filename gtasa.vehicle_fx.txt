// Script by BengbuGuards
{$CLEO .csa}
0000: NOP
script_name "VEHFX"
const
    KMB_MARIJUANA = -201
    FIRELA_MAX_ANGLE = 2500
end

0DD0: 10@ = get_label_addr @CPools_GetVehicle
0DD1: 10@ = get_func_addr_by_cstr_name 10@

0DD0: 11@ = get_label_addr @CPlane_ProcessControl
0DD1: 11@ = get_func_addr_by_cstr_name 11@
11@ -= 1
11@ += 0x3A // MOVS  R0, #0x66
0DD9: write_mem_addr 11@ value 0xBF00 size 2 add_ib 0 protect 1 // NOP
11@ += 2 // BLX  j__ZN4CHID10IsReleasedE10HIDMapping
0DD9: write_mem_addr 11@ value 0xE8C4F422 size 4 add_ib 0 protect 1 // BLX  j__ZN4CPad12HornJustDownEv

0DD0: 11@ = get_label_addr @CPlane_ProcessControlInputs
0DD1: 11@ = get_func_addr_by_cstr_name 11@
11@ -= 1
11@ += 0x61A // STRH.W  R1, [R4,#0x880] 
0DD9: write_mem_addr 11@ value 0x8000F3AF size 4 add_ib 0 protect 1 // NOP.W
11@ += 0x60  // STRH.W  R1, [R4,#0x880]
0DD9: write_mem_addr 11@ value 0x8000F3AF size 4 add_ib 0 protect 1 // NOP.W 

0DD0: 11@ = get_label_addr @CVehicle_ProcessSirenAndHorn
0DD1: 11@ = get_func_addr_by_cstr_name 11@
11@ -= 1
11@ += 0x7A // MOVS  R1, #0 
0DD9: write_mem_addr 11@ value 0x8000F3AF size 4 add_ib 0 protect 1 // NOP.W
11@ += 4  // CMP.W  R1, R0,LSR#7
0DD9: write_mem_addr 11@ value 0x2100 size 2 add_ib 0 protect 1 // MOVS  R1, #0 
11@ += 0x32 // BICGT.W  R2, R2, #0x8000
0DD9: write_mem_addr 11@ value 0x8000F3AF size 4 add_ib 0 protect 1 // NOP.W

{0DD0: 11@ = get_label_addr @CVehicleModelInfo_ms_vehicleDescs
0DD1: 11@ = get_func_addr_by_cstr_name 11@
11@ -= 1
11@ += 20 // DCD off_6874CC
0DD8: 11@ = read_mem_addr 11@ size 3 add_ib 0 // DCD aBoatMovingHi
0DD8: 11@ = read_mem_addr 11@ size 3 add_ib 1 // DCB "boat_moving_hi",0
11@ += 0xB // "_hi",0}
0DD9: write_mem_addr 0x6107D9 value 0 size 3 add_ib 1 protect 1 // "boat_moving"  0x6107D9 

0DD0: 11@ = get_label_addr @CPad_HornJustDown
0DD1: 11@ = get_func_addr_by_cstr_name 11@
11@ -= 1
11@ += 0x14 // MOVS  R0, #0 & POP  {R4,R6,R7,PC}
0DD9: write_mem_addr 11@ value 0xBDD02000 size 4 add_ib 0 protect 1

2@ = 0
3@ = 0
8@ = 0
9@ = 0
TIMERA = 0
TIMERB = 0
0247: load_model KMB_MARIJUANA 
038B: load_requested_models 

while true
    wait 0 
    if and
        00DF: actor $PLAYER_ACTOR driving
        84C8: not actor $PLAYER_ACTOR driving_flying_vehicle
        84A7: not actor $PLAYER_ACTOR driving_boat
        80DD: not actor $PLAYER_ACTOR driving_car_with_model #RHINO
        80DD: not actor $PLAYER_ACTOR driving_car_with_model #BMX
        80DD: not actor $PLAYER_ACTOR driving_car_with_model #MTBIKE
        80DD: not actor $PLAYER_ACTOR driving_car_with_model #BIKE
    then
        03C0: 0@ = actor $PLAYER_CHAR car
        0DD3: context_set_reg 0 value 0@
        0DD2: context_call_func 10@
        0DD4: 1@ = context_get_reg 0
        1@ += 0x3A // byte m_nTypeAndStatus
        0DD8: 1@ = read_mem_addr 1@ size 1 add_ib 0
        if
            7 >= 1@
        then
            0DD4: 1@ = context_get_reg 0
            1@ += 0x4B2 // [bit 4,5] BYTE m_nOverrideLights
            if
                0A52: is_widget_released 160 
            then
                0DD8: 1@ = read_mem_addr 1@ size 1 add_ib 0
                if
                    08B7: test 1@ bit 4
                then
                    067F: set_car 0@ lights 1 // FORCE_CAR_LIGHTS_OFF
                    00BA: show_text_styled GXT "OFF" time 1000 style 5
                else
                    067F: set_car 0@ lights 2 // FORCE_CAR_LIGHTS_ON
                    00BA: show_text_styled GXT "ON" time 1000 style 5
                end
            else
                if
                    0A54: is_widget_swiped 160
                then
                    0DD8: 1@ = read_mem_addr 1@ size 1 add_ib 0
                    if or
                        08B7: test 1@ bit 3
                        08B7: test 1@ bit 4
                    then
                        067F: set_car 0@ lights 0 // NO_CAR_LIGHT_OVERRIDE
                        00BA: show_text_styled GXT "AUTO" time 1000 style 5
                    else
                        00BF: 1@ = current_time_hours, 39@ = current_time_minutes 
                        if or
                            1@ >= 22
                            5 >= 1@
                        then
                            067F: set_car 0@ lights 2 // FORCE_CAR_LIGHTS_ON
                            00BA: show_text_styled GXT "ON" time 1000 style 5
                        else
                            067F: set_car 0@ lights 1 // FORCE_CAR_LIGHTS_OFF
                            00BA: show_text_styled GXT "OFF" time 1000 style 5
                        end
                    end
                end 
            end
        end
    end
    if or
        00DD: actor $PLAYER_ACTOR driving_car_with_model #STUNT
        00DD: actor $PLAYER_ACTOR driving_car_with_model #CROPDUST
        00DD: actor $PLAYER_ACTOR driving_car_with_model #POLMAV
    then
        if
            9@ == 0
        then
            0DD9: write_mem_addr 11@ value 0xBF00E02D size 4 add_ib 0 protect 1 // B  loc_3FA6C2 & NOP
            9@ = 1
        end
        continue
    else
        if
            9@ == 1
        then
            0DD9: write_mem_addr 11@ value 0xBDD02000 size 4 add_ib 0 protect 1 // MOVS  R0, #0 & POP  {R4,R6,R7,PC}
            9@ = 0
        end
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #HYDRA
    then
        IS_PLAYER_REALLY_IN_CAR()
        if
            0@ == 1
        jf continue
        if
            0A53: is_widget_doubletapped 04
        then
            03C0: 0@ = actor $PLAYER_CHAR car
            0DD3: context_set_reg 0 value 0@
            0DD2: context_call_func 10@
            0DD4: 0@ = context_get_reg 0
            0@ += 0x880 // short m_wMiscComponentAngle
            0DD8: 1@ = read_mem_addr 0@ size 2 add_ib 0
            if
                1@ == 5000
            then
                1@ = 0
            else
                1@ = 5000
            end
            0DD9: write_mem_addr 0@ value 1@ size 2 add_ib 0 protect 0
        end
        continue
    end 
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #FIRELA
    then
        IS_PLAYER_REALLY_IN_CAR()
        if
            0@ == 1
        jf continue
        03C0: 0@ = actor $PLAYER_CHAR car
        0DD3: context_set_reg 0 value 0@
        0DD2: context_call_func 10@
        0DD4: 0@ = context_get_reg 0
        0@ += 0x880 // short m_wMiscComponentAngle
        0DD8: 1@ = read_mem_addr 0@ size 2 add_ib 0 
        if and
            1@ < FIRELA_MAX_ANGLE
            8@ <> 20
        then
            0A5E: set_widget 13 value 0.0 // float 
        else
            0A5E: set_widget 13 value 2500.0 // float 
        end
        if and
            1@ < FIRELA_MAX_ANGLE
            8@ <> 20 // Angle to add 
        then
            if
                0A52: is_widget_released 13
            then
                8@ = 20
            end
        else
            if
                0A52: is_widget_released 13
            then
                8@ = -20
            end
        end
        if and
            TIMERB > 8
            8@ <> 0
        then
            005A: 1@ += 8@ // (int)
            if
                1@ >= FIRELA_MAX_ANGLE
            then
                8@ = 0
                1@ = FIRELA_MAX_ANGLE
            else
                if
                    1@ <= 0
                then
                    8@ = 0
                    1@ = 0
                end
            end
            0DD9: write_mem_addr 0@ value 1@ size 2 add_ib 0 protect 0
            TIMERB = 0
        end
        continue
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #CEMENT
    then
        IS_PLAYER_REALLY_IN_CAR()
        if
            0@ == 1
        jf continue
        if
            0A52: is_widget_released 25 
        then
            if
                2@ == 0
            then  
                03C0: 0@ = actor $PLAYER_CHAR car
                066C: 2@ = attach_particle "CEMENT" to_car 0@ with_offset 0.0 -4.4 0.0 rotation 0.0 -1.0 0.0 type 1 
                064C: make_particle 2@ visible
            else
                064E: stop_particle 2@
                2@ = 0
            end
        end
        continue
    else 
        if
            2@ <> 0
        then
            064E: stop_particle 2@
            2@ = 0
        end
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #COMBINE
    then
        03C0: 0@ = actor $PLAYER_CHAR car
        0DD3: context_set_reg 0 value 0@
        0DD2: context_call_func 10@
        0DD4: 0@ = context_get_reg 0
        0@ += 0x75B // CColPoint m_wheelColPoint[0].byte m_nSurfaceTypeA
        0DD8: 1@ = read_mem_addr 0@ size 1 add_ib 0
        if or
            1@ == 129 // SURFACE_P_FORESTSTICKS
            1@ == 40 // SURFACE_CORNFIELD
        then
            3@ = 1
        else
            3@ = 0 
            TIMERA = 0
        end
        if and
           3@ == 1
           TIMERA > 2250
        then
            03C0: 0@ = actor $PLAYER_CHAR car
            02E3: 4@ = car 0@ speed 
            if
                4@ > 5
            then
                0407: store_coords_to 4@ 5@ 6@ from_car 0@ with_offset -1.3 -3.3 1.3 
                0107: 7@ = create_object KMB_MARIJUANA at 4@ 5@ 6@ 
                0392: make_object 7@ moveable 1 
                0381: throw_object 7@ velocity_in_direction 0.0 0.0 -1.0 
                0208: 4@ = random_float_in_ranges 0.0 180.0 
                034D: rotate_object 7@ from_angle 4@ to_angle 10.0 flag 0 
                050E: set_object 7@ no_collision_with_car 0@ 
                01C4: remove_references_to_object 7@ // This object will now disappear when the player looks away 
            end
            TIMERA = 0 
        end
        continue
    else
        if
            3@ <> 0
        then
            3@ = 0
        end
    end
end


:IS_PLAYER_REALLY_IN_CAR // out: 0@
03C0: 0@ = actor $PLAYER_CHAR car
0DD3: context_set_reg 0 value 0@
0DD2: context_call_func 10@
0DD4: 0@ = context_get_reg 0
0@ += 0x3A // byte m_nTypeAndStatus
0DD8: 0@ = read_mem_addr 0@ size 1 add_ib 0
if
    7 >= 0@
then
    0@ = 1
else
    0@ = 0
end
return

:CPools_GetVehicle
hex
    "_ZN6CPools10GetVehicleEi" 00
end

:CPlane_ProcessControl
hex
    "_ZN6CPlane14ProcessControlEv" 00
end

:CPlane_ProcessControlInputs
hex
    "_ZN6CPlane20ProcessControlInputsEh" 00
end

:CVehicle_ProcessSirenAndHorn
hex
    "_ZN8CVehicle19ProcessSirenAndHornEb" 00
end

{:CVehicleModelInfo_ms_vehicleDescs
hex
    "_ZN17CVehicleModelInfo15ms_vehicleDescsE" 00
end}

:CPad_HornJustDown
hex
    "_ZN4CPad12HornJustDownEv" 00
end