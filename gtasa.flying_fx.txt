// Script by BengbuGuards
{$CLEO .csa}
0000: NOP
script_name "FLYFX"
const
    KMB_MARIJUANA = -201
    FIRELA_MAX_ANGLE = 2500
end

0DD0: 10@ = get_label_addr @CPools_GetVehicle
0DD1: 10@ = get_func_addr_by_cstr_name 10@
0DD0: 11@ = get_label_addr @CPlane_ProcessControlInputs
0DD1: 11@ = get_func_addr_by_cstr_name 11@
11@ -= 1
11@ += 0x61A // STRH.W  R1, [R4,#0x880] 
0DD9: write_mem_addr 11@ value 0x8000F3AF size 4 add_ib 0 protect 1 // NOP.W
11@ += 0x60  // STRH.W  R1, [R4,#0x880]
0DD9: write_mem_addr 11@ value 0x8000F3AF size 4 add_ib 0 protect 1 // NOP.W 
0DD0: 12@ = get_label_addr @CWidget_SetWidgetValue
0DD1: 12@ = get_func_addr_by_cstr_name 12@
0DD0: 13@ = get_label_addr @CTouchInterface_m_pWidgets
0DD1: 13@ = get_func_addr_by_cstr_name 13@
13@ += 52 // 4*13
2@ = 0
3@ = 0
8@ = 0
TIMERA = 0
TIMERB = 0
0247: load_model KMB_MARIJUANA 
038B: load_requested_models 

while true
    wait 0 
    if or
        00DD: actor $PLAYER_ACTOR driving_car_with_model #STUNT
        00DD: actor $PLAYER_ACTOR driving_car_with_model #CROPDUST
    then
        if
            0A52: is_widget_released 07
        then
            03C0: 0@ = actor $PLAYER_CHAR car
            0DD3: context_set_reg 0 value 0@
            0DD2: context_call_func 10@
            0DD4: 0@ = context_get_reg 0
            0@ += 0xA14 // bool m_bSmokeEjectorEnabled
            0DD8: 1@ = read_mem_addr 0@ size 1 add_ib 0
            reverse_1at() 
            0DD9: write_mem_addr 0@ value 1@ size 1 add_ib 0 protect 0 
        end
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #POLMAV
    then
        if
            0A52: is_widget_released 07
        then
            03C0: 0@ = actor $PLAYER_CHAR car
            0DD3: context_set_reg 0 value 0@
            0DD2: context_call_func 10@
            0DD4: 0@ = context_get_reg 0
            0@ += 0xA25 // bool m_bSearchLightEnabled
            0DD8: 1@ = read_mem_addr 0@ size 1 add_ib 0
            reverse_1at()
            0DD9: write_mem_addr 0@ value 1@ size 1 add_ib 0 protect 0
        end
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #HYDRA
    then
        if
            0A53: is_widget_doubletapped 04
        then
            03C0: 0@ = actor $PLAYER_CHAR car
            0DD3: context_set_reg 0 value 0@
            0DD2: context_call_func 10@
            0DD4: 0@ = context_get_reg 0
            0@ += 0x880 // short m_wMiscComponentAngle
            0DD8: 1@ = read_mem_addr 0@ size 2 add_ib 0
            if
                1@ == 5000
            then
                1@ = 0
            else
                1@ = 5000
            end
            0DD9: write_mem_addr 0@ value 1@ size 2 add_ib 0 protect 0
        end
    end 
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #FIRELA
    then
        03C0: 0@ = actor $PLAYER_CHAR car
        0DD3: context_set_reg 0 value 0@
        0DD2: context_call_func 10@
        0DD4: 0@ = context_get_reg 0
        0@ += 0x880 // short m_wMiscComponentAngle
        0DD8: 1@ = read_mem_addr 0@ size 2 add_ib 0 
        0DD8: 9@ = read_mem_addr 13@ size 4 add_ib 0 // CWidgetButtonAutoHydraulics*
        0DD3: context_set_reg 0 value 9@
        if and
            1@ < FIRELA_MAX_ANGLE
            8@ <> 20
        then
            0DD3: context_set_reg 1 value 0.0
        else
            0DD3: context_set_reg 1 value 2500.0
        end
        0DD2: context_call_func 12@
        if and
            1@ < FIRELA_MAX_ANGLE
            8@ <> 20 // Angle to add 
        then
            if
                0A52: is_widget_released 13
            then
                8@ = 20
            end
        else
            if
                0A52: is_widget_released 13
            then
                8@ = -20
            end
        end
        if and
            TIMERB > 8
            8@ <> 0
        then
            005A: 1@ += 8@ // (int)
            if
                1@ >= FIRELA_MAX_ANGLE
            then
                8@ = 0
                1@ = FIRELA_MAX_ANGLE
                
            else
                if
                    1@ <= 0
                then
                    8@ = 0
                    1@ = 0
                end
            end
            0DD9: write_mem_addr 0@ value 1@ size 2 add_ib 0 protect 0
            TIMERB = 0
        end
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #CEMENT
    then
        if
            0A52: is_widget_released 07 
        then
            if
                2@ == 0
            then  
                03C0: 0@ = actor $PLAYER_CHAR car
                066C: 2@ = attach_particle "CEMENT" to_car 0@ with_offset 0.0 -4.4 0.0 rotation 0.0 -1.0 0.0 type 1 
                064C: make_particle 2@ visible
            else
                064E: stop_particle 2@
                2@ = 0
            end
        end
    else 
        if
            2@ <> 0
        then
            064E: stop_particle 2@
            2@ = 0
        end
    end
    if
        00DD: actor $PLAYER_ACTOR driving_car_with_model #COMBINE
    then
        03C0: 0@ = actor $PLAYER_CHAR car
        0DD3: context_set_reg 0 value 0@
        0DD2: context_call_func 10@
        0DD4: 0@ = context_get_reg 0
        0@ += 0x75B // CColPoint m_wheelColPoint[0].byte m_nSurfaceTypeA
        0DD8: 1@ = read_mem_addr 0@ size 1 add_ib 0
        if or
            1@ == 129 // SURFACE_P_FORESTSTICKS
            1@ == 40 // SURFACE_CORNFIELD
        then
            3@ = 1
        else
            3@ = 0 
            TIMERA = 0
        end
        if and
           3@ == 1
           TIMERA > 2250
        then
            03C0: 0@ = actor $PLAYER_CHAR car
            02E3: 4@ = car 0@ speed 
            if
                4@ > 5
            then
                0407: store_coords_to 4@ 5@ 6@ from_car 0@ with_offset -1.3 -3.3 1.3 
                0107: 7@ = create_object KMB_MARIJUANA at 4@ 5@ 6@ 
                0392: make_object 7@ moveable 1 
                0381: throw_object 7@ velocity_in_direction 0.0 0.0 -1.0 
                0208: 4@ = random_float_in_ranges 0.0 180.0 
                034D: rotate_object 7@ from_angle 4@ to_angle 10.0 flag 0 
                050E: set_object 7@ no_collision_with_car 0@ 
                01C4: remove_references_to_object 7@ // This object will now disappear when the player looks away 
            end
            TIMERA = 0 
        end
    else
        if
            3@ <> 0
        then
            3@ = 0
        end
    end
end

:reverse_1at
if
    1@ == 0
then
    1@ = 1
else
    1@ = 0
end
return

:CPools_GetVehicle
hex
    "_ZN6CPools10GetVehicleEi" 00
end

:CPlane_ProcessControlInputs
hex
    "_ZN6CPlane20ProcessControlInputsEh" 00
end

:CWidget_SetWidgetValue
hex
    "_ZN7CWidget14SetWidgetValueEf" 00
end

:CTouchInterface_m_pWidgets
hex
    "_ZN15CTouchInterface10m_pWidgetsE" 00
end